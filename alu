//Date: 03/06/2019
//Author: TrongTran
//Design CPU 32 bit

module alu (ALUSel, ALUOut, Reg_1, Reg_2);


parameter CONTROL_WIDTH = 4;
parameter DATA_WIDTH    = 32;

input   [CONTROL_WIDTH-1:0] ALUSel;
input   [DATA_WIDTH-1:0]    Reg_1;
input   [DATA_WIDTH-1:0]    Reg_2;
output  [DATA_WIDTH-1:0]    ALUOut;
reg     [DATA_WIDTH-1:0]    ALUOut;
wire    [DATA_WIDTH-1:0]    shl_ALUOut;
wire    [DATA_WIDTH-1:0]    sum;
wire    [DATA_WIDTH-1:0]    diff;
wire    [DATA_WIDTH-1:0]    and_ALUOut;
wire    [DATA_WIDTH-1:0]    or_ALUOut;
wire    [DATA_WIDTH-1:0]    xor_ALUOut;
wire    [DATA_WIDTH-1:0]    shr_ALUOut;
wire    [DATA_WIDTH-1:0]    shr_ALUOut_A;
wire    [DATA_WIDTH-1:0]    less_than_signed;
wire    [DATA_WIDTH-1:0]    less_than_unsigned;
wire                        sign;


assign sum          = Reg_1 + Reg_2;
assign diff         = Reg_1 - Reg_2;
assign and_ALUOut   = Reg_1 & Reg_2;
assign or_ALUOut    = Reg_1 | Reg_2;
assign xor_ALUOut   = Reg_1 ^ Reg_2;
assign less_than_signed    = $signed(Reg_1) < $signed(Reg_2);
assign less_than_unsigned = Reg_1 < Reg_2;
assign shl_ALUOut   = Reg_1 << Reg_2[4:0];
assign sign         = Reg_1[31];
assign shr_ALUOut   = Reg_1 >> Reg_2[4:0];
assign shr_ALUOut_A   = { {32{sign}}, Reg_1 } >> Reg_2[4:0];


always @ (*) begin
   case (ALUSel)
      4'b0000: ALUOut = sum;
      4'b1000: ALUOut = diff;
      4'b0111: ALUOut = and_ALUOut;
      4'b0110: ALUOut = or_ALUOut;
      4'b0100: ALUOut = xor_ALUOut;
      4'b0001: ALUOut = shl_ALUOut;
      4'b0101: ALUOut = shr_ALUOut;
      4'b1101: ALUOut = shr_ALUOut_A;
      4'b0010: ALUOut = less_than_signed;
      4'b0011: ALUOut = less_than_unsigned;
      4'b1111: ALUOut = Reg_2;
      default: ALUOut = Reg_1;
   endcase
end
endmodule
